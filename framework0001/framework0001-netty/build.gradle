apply plugin: 'application'
apply plugin: 'maven-publish'

sourceCompatibility = 17
mainClassName = 'pama1234.net.netty.test.MainTest'
eclipse.project.name = appName + '-netty'

dependencies {
  implementation project(':server-framework')

  testImplementation 'junit:junit:4.13.1'
  testImplementation 'com.carrotsearch:junit-benchmarks:0.7.2'

  implementation "io.netty:netty-buffer:$nettyVersion"
  implementation "io.netty:netty-transport:$nettyVersion"
  implementation "io.netty:netty-transport-native-epoll:$nettyVersion"
  implementation "io.netty:netty-handler:$nettyVersion"

  implementation "com.esotericsoftware:kryo:$kryoVersion"
}

jar {
  archiveBaseName.set(eclipse.project.name)
  // the duplicatesStrategy matters starting in Gradle 7.0; this setting works.
  duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
  dependsOn configurations.runtimeClasspath
  from {
    configurations.runtimeClasspath.collect {
      it.isDirectory() ? it : zipTree(it)
    }
  }
  // these "exclude" lines remove some unnecessary duplicate files in the output JAR.
  exclude('META-INF/INDEX.LIST', 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA')
  dependencies {
    exclude('META-INF/INDEX.LIST', 'META-INF/maven/**')
  }
  // setting the manifest makes the JAR runnable.
  manifest {
    attributes 'Main-Class': project.mainClassName
  }
  // this last step may help on some OSes that need extra instruction to make runnable JARs.
  doLast {
    file(archiveFile).setExecutable(true, false)
  }
}

// Equivalent to the jar task; here for compatibility with gdx-setup.
task dist(dependsOn: [jar]) { }

publishing {
  publications {
    maven(MavenPublication) {
      groupId = 'pama1234'
      artifactId = 'framework-netty'
      version = "$pama1234PublishVersion"
      from components.java

      pom {
        name = 'pama1234-framework-netty'
        description = 'netty framework'
        url = 'https://github.com/pama1234/just-some-other-libgdx-game'
        scm {
          url = 'https://github.com/pama1234/just-some-other-libgdx-game'
        }
      }
    }
  }
}