apply plugin: 'application'
apply plugin: 'maven-publish'

sourceCompatibility = 17
mainClassName = 'pama1234.util.yaml.MainTest'
eclipse.project.name = appName + '-processing'

dependencies {
  api project(':framework')

  api 'org.jogamp.jogl:jogl-all:2.4.0'
  api 'org.jogamp.gluegen:gluegen-rt-main:2.4.0-rc-20230201'

  api files('lib/jna-platform.jar')
  api files('lib/jna.jar')
  api files('lib/VideoExport.jar')

  // implementation "javax.vecmath:vecmath:$vecmathVersion"
  // implementation "com.esotericsoftware:kryo:$kryoVersion"
  // implementation "org.yaml:snakeyaml:$snakeYamlVersion"

  // implementation "io.netty:netty-all:$nettyVersion"
}

jar {
  archiveBaseName.set(eclipse.project.name)
  // the duplicatesStrategy matters starting in Gradle 7.0; this setting works.
  duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
  dependsOn configurations.runtimeClasspath
  from {
    configurations.runtimeClasspath.collect {
      it.isDirectory() ? it : zipTree(it)
    }
  }
  // these "exclude" lines remove some unnecessary duplicate files in the output JAR.
  exclude('META-INF/INDEX.LIST', 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA')
  dependencies {
    exclude('META-INF/INDEX.LIST', 'META-INF/maven/**')
  }
  // setting the manifest makes the JAR runnable.
  manifest {
    attributes 'Main-Class': project.mainClassName
  }
  // this last step may help on some OSes that need extra instruction to make runnable JARs.
  doLast {
    file(archiveFile).setExecutable(true, false)
  }
}

// Equivalent to the jar task; here for compatibility with gdx-setup.
task dist(dependsOn: [jar]) { }

publishing {
  publications {
    maven(MavenPublication) {
      groupId = 'pama1234'
      artifactId = 'framework-processing'
      version = "$pama1234PublishVersion"
      from components.java

      pom {
        name = 'pama1234-framework-processing'
        description = 'processing framework'
        url = 'https://github.com/pama1234/just-some-other-libgdx-game'
        scm {
          url = 'https://github.com/pama1234/just-some-other-libgdx-game'
        }
      }
    }
  }
}